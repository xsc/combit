(ns ^{ :doc "Tests for Combit Combinations Operator."
       :author "Yannick Scherer" }
  combit.combine-test
  (:use clojure.test
        combit.component
        combit.input-output))

(deftest transform-tests
  (testing "Behaviour of Functions generated by `transform-outputs->>`"
    (let [dta [[1 2 3 4] [5 6]]
          odta [[0 0 0 0] [0 0]]
          in0 (input-data (first dta) 4)
          in1 (input-data (second dta) 2)
          out0 (output-data 0 4)
          out1 (output-data 1 2)
          cdta [7 8 9 10]
          c0 (const-data cdta)]
      (is (= (transform-outputs->> odta (in0) (out0)) [(first dta) (second odta)]))
      (is (= (transform-outputs->> odta (c0) (out0)) [cdta (second odta)]))
      (is (= (transform-outputs->> odta (in1) (out1)) [(first odta) (second dta)]))

      (are [ispec ospec r] (= (transform-outputs->> odta (in0 ispec) (out0 ospec)) [r (second odta)])
           0 0 [1 0 0 0]
           0 1 [0 1 0 0]
           0 2 [0 0 1 0]
           0 3 [0 0 0 1]
           1 0 [2 0 0 0]
           1 1 [0 2 0 0]
           1 2 [0 0 2 0]
           1 3 [0 0 0 2]
           2 0 [3 0 0 0]
           2 1 [0 3 0 0]
           2 2 [0 0 3 0]
           2 3 [0 0 0 3]
           3 0 [4 0 0 0]
           3 1 [0 4 0 0]
           3 2 [0 0 4 0]
           3 3 [0 0 0 4]
           [0 2] [1 3] [0 1 0 3]
           [0 2] [3 1] [0 3 0 1]
           [2 0] [1 3] [0 3 0 1]

           [1 2 3] [3 0 1] [3 4 0 2])
      (are [ispec ospec r] (= (transform-outputs->> odta (in1 ispec) (out1 ospec)) [(first odta) r])
           0 0 [5 0]
           0 1 [0 5]
           1 0 [6 0]
           1 1 [0 6]
           [0 1] [1 0] [6 5]
           [1 0] [0 1] [6 5]))))

(deftest curry-tests
  (testing "Currying."
    (let [f0 (->
               (fn [a b c d]
                 (/ (+ a (* b c) 1) d))
               (curry 4))]
      (is (= ((f0) 1 2 3 4) 2))
      (is (= ((f0 1) 2 3 4) 2))
      (is (= ((f0 1 2) 3 4) 2))
      (is (= ((f0 1 2 3) 4) 2))
      (is (= (f0 1 2 3 4) 2))

      (is (= (((f0 1) 2) 3 4) 2))
      (is (= ((((f0 1) 2) 3) 4) 2))
      (is (= (((f0 1 2) 3) 4) 2)))))

(deftest concatenation-tests
  (testing "Functions generated by `conc`."
    (testing "First parameter is value."
      (let [f1 (conc [6 0] first)
            f2 (conc [6 0] (comp constantly (juxt first first)) +)]
        (is (= (f1) [6]))
        (is (= (f2) [12]))))
    (testing "First parameter is constantly-function."
      (let [f (conc (constantly [7 3]) +)]
        (is (= (f) [10]))))))
